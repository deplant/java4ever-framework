plugins {
    id "java-library"
    id "maven-publish"
    id "signing"
}

// Global variables for all modules
//ext {
    // dependencies

//}

group v_groupId
version v_version

repositories {
    mavenLocal()
    mavenCentral()
}


sourceSets {
    main {
        java {
            srcDirs = ['src/main/java', 'src/gen/java']
        }
    }
}

dependencies {

    // RUNTIME **************************
    api "tech.deplant.java4ever:java4ever-binding:$v_version" // java4ever-binding

    // TESTS **************************

    // SLF4J (Simple provider for tests)
    testImplementation "org.slf4j:slf4j-jdk-platform-logging:$slf4jVersion" // SLF4J Bridge to JDK9 System.Logger
    testImplementation "org.slf4j:slf4j-simple:$slf4jVersion"

    // JUnit 5
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"

}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/" + v_repository)
            credentials {
                username = System.getenv("USERNAME")
                password = System.getenv("GH_PACKAGES_UPLOAD_TOKEN")
            }
        }
        maven {
            name = "Sonatype"
            url = uri("https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/")
            credentials {
                username = System.getenv("OSSRH_USERNAME")
                password = System.getenv("OSSRH_PASSWORD")
            }
        }
    }
    publications {
        framework(MavenPublication) {
            groupId = v_groupId
            artifactId = v_artifactId
            version = v_version

            from components.java

            pom {
                name = v_artifactId
                description = 'Development framework for Everscale DApp development'
                url = "https://github.com/" + v_repository
//                properties = [
//                        myProp          : "value",
//                        "prop.with.dots": "anotherValue"
//                ]
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = v_developerId
                        name = v_developerName
                        email = v_developerEmail
                        organization = v_org
                        organizationUrl = v_url
                    }
                }
                scm {
                    connection = "scm:git:git://github.com/" + v_repository + ".git"
                    developerConnection = "scm:git:ssh://github.com/" + v_repository + ".git"
                    url = "http://github.com/" + v_repository
                }
            }
        }
    }
}

signing {
    sign publishing.publications.framework
}

java {
    withJavadocJar()
    withSourcesJar()
}

// Task for JavaPoet generation of EVER-SDK API
task generateEverscaleContracts(type: JavaExec) {
    group = "other"
    description = "Generates Java Wrappers for Everscale contracts: Wallets, TIP3, TIP4, Givers and so on"
    classpath = sourceSets.test.runtimeClasspath
    mainClass = "tech.deplant.java4ever.frtest.codegen.ContractGenerator"
}

tasks.withType(JavaCompile).configureEach {
    sourceCompatibility = JavaVersion.VERSION_20
    targetCompatibility = JavaVersion.VERSION_20
    javaCompiler = javaToolchains.compilerFor {
        languageVersion = JavaLanguageVersion.of(20)
    }
    options.compilerArgs += "--enable-preview"
    options.compilerArgs += "--add-modules=jdk.incubator.concurrent"
}

tasks.withType(JavaExec).configureEach {
    javaLauncher = javaToolchains.launcherFor {
        languageVersion = JavaLanguageVersion.of(20)
    }
    jvmArgs += "--enable-preview"
    jvmArgs += "--add-modules=jdk.incubator.concurrent"
}

tasks.withType(Javadoc).configureEach {
    javadocTool = javaToolchains.javadocToolFor {
        languageVersion = JavaLanguageVersion.of(20)
    }
    failOnError false
    options.addBooleanOption('html5', true)
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('charSet', 'UTF-8')
    options.addBooleanOption('-enable-preview', true)
    options.addStringOption('-release', '20')
    options.addBooleanOption('-ignore-source-errors', true)
    options.addStringOption('Xdoclint:none', '-quiet')
}

tasks.withType(Test) {
    javaLauncher = javaToolchains.launcherFor {
        languageVersion = JavaLanguageVersion.of(20)
    }
    useJUnitPlatform()
    jvmArgs += "--enable-preview"
    jvmArgs += "--add-modules=jdk.incubator.concurrent"
    jvmArgs += "--enable-native-access=java4ever.binding"
}