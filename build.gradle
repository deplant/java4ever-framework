plugins {
    id "java-library"
    id "maven-publish"
    id "signing"
}

// Global variables for all modules
ext {
    // dependencies
    slf4jVersion = "2.0.3"
    junitVersion = "5.9.1"

    // details
    v_version = "1.5.0"
    v_groupId = "tech.deplant.java4ever"
    v_artifactId = "java4ever-framework"
    v_repository = "deplant/java4ever-framework"
    v_developerId = "cassandrus"
    v_developerName = "Dima Yankin"
    v_developerEmail = "cassandrus@protonmail.com"
}

group v_groupId
version v_version

repositories {
    mavenCentral()
    mavenLocal()
}


sourceSets {
    main {
        java {
            srcDirs = ['src/main/java', 'src/gen/java']
        }
    }
}

dependencies {

    // RUNTIME **************************

    api "tech.deplant.java4ever:java4ever-binding:$v_version" // java4ever-binding
    api "tech.deplant.java4ever:java4ever-utils:$v_version" // java4ever-utils
    implementation "org.slf4j:slf4j-jdk-platform-logging:$slf4jVersion" // SLF4J Bridge to JDK9 System.Logger

    // TESTS **************************

    // SLF4J (Simple provider for tests)
    testImplementation "org.slf4j:slf4j-simple:$slf4jVersion"

    // JUnit 5
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"

}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/" + v_repository)
            credentials {
                username = System.getenv("USERNAME")
                password = System.getenv("GH_PACKAGES_UPLOAD_TOKEN")
            }
        }
        maven {
            name = "Sonatype"
            url = uri("https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/")
            credentials {
                username = System.getenv("OSSRH_USERNAME")
                password = System.getenv("OSSRH_PASSWORD")
            }
        }
    }
    publications {
        framework(MavenPublication) {
            groupId = v_groupId
            artifactId = v_artifactId
            version = v_version

            from components.java

            pom {
                name = v_artifactId
                description = 'Development framework for Everscale DApp development'
                url = "https://github.com/" + v_repository
//                properties = [
//                        myProp          : "value",
//                        "prop.with.dots": "anotherValue"
//                ]
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = v_developerId
                        name = v_developerName
                        email = v_developerEmail
                        organization = "deplant"
                        organizationUrl = "https://deplant.tech"
                    }
                }
                scm {
                    connection = "scm:git:git://github.com/" + v_repository + ".git"
                    developerConnection = "scm:git:ssh://github.com/" + v_repository + ".git"
                    url = "http://github.com/" + v_repository
                }
            }
        }
    }
}

signing {
    sign publishing.publications.framework
}

javadoc {
    options.addBooleanOption('html5', true)
    options.addBooleanOption('-enable-preview', true)
    options.addStringOption('-release', '19')
}

java {
    withJavadocJar()
    withSourcesJar()
}

compileJava {
    sourceCompatibility = JavaVersion.VERSION_19
    targetCompatibility = JavaVersion.VERSION_19
}

test {
    useJUnitPlatform()
    jvmArgs += "--enable-preview"
    jvmArgs += "--add-modules=jdk.incubator.concurrent"
    jvmArgs += "--enable-native-access=java4ever.binding"
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs += "--enable-preview"
    options.compilerArgs += "--add-modules=jdk.incubator.concurrent"
}

tasks.withType(JavaExec).configureEach {
    jvmArgs += "--enable-preview"
    jvmArgs += "--add-modules=jdk.incubator.concurrent"
}

tasks.withType(Test) {
    jvmArgs += "--enable-preview"
    jvmArgs += "--add-modules=jdk.incubator.concurrent"
}